layout (location = 0) in vec3 aPos;

uniform float pointSizeReal;
uniform float pointScale;
uniform mat4 pvm;
uniform vec2 windowSize;
uniform vec3 voxelScale;
out vec3 posW;

void main()
{
 	
 	//projected (on screen) vertex position of the center
 	gl_Position = pvm * vec4( aPos, 1.0 );
 	
 	//let's shift it to the side 
 	//for some value in x in the screen coordinates,
 	//so we stay in the same z-plane
 	
 	vec4 shift = gl_Position;
 	shift.x = shift.x + 100.0;
 	//why 100? why not?

	// let's go back to original (voxel) coordinates
	// and get now the line in x direction of the screen
	// (in the same z plane)
	
 	mat4 invPVM = inverse(pvm);
 	shift = invPVM * shift;
 	
 	//move to space units
 	shift.xyz = voxelScale * shift.xyz;
 	vec3 pos_scale = voxelScale * aPos;

 	//normalize length along this direction
 	//in the absolute units
 	shift.xyz = shift.xyz - pos_scale;

 	shift.xyz = shift.xyz * 0.5 * pointSizeReal / length(shift.xyz);
 	
 	shift.xyz = shift.xyz + pos_scale;
 	
 	//move back to voxel units
 	
 	shift.xyz = shift.xyz / voxelScale;
 	
 	//project it back to the screen
 	shift = pvm * shift;
 	
 	//calculate final required radius in pixels
 	//(we need to multiply it by w to do a proper projection)
 	float dRadius = abs(windowSize.x*(shift.x/shift.w-gl_Position.x/gl_Position.w));
 	
 	//final point size,
 	//let's make sure it is not zero
    gl_PointSize =  max(dRadius/pointScale,1.0);
    posW = aPos;

}
